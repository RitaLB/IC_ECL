*tp_not((A+B).C)

*PARAMETROS
.include 7nm_TT.pm

* Declarando Fontes de tensão
Vvdd vdd gnd 0.7

* Declaração das fontes
Va a gnd PWL (0n 0 6n 0 6.01n 0.7 8n 1 8.01n 0 16n 0 16.01n 0.7 22n 0.7)
Vb b gnd PWL (0n 0 2n 0 2.01n 0.7 4n 0.7 4.01n 0 10n 0 10.01n 0.7 16n 0.7 16.01n 0 22n 0)
Vc c gnd PWL (0n 0.7 10n 0.7 10.01n 0 12n 0 12.01n 0.7 14n 0.7 14.01n 0 18n 0 18.01n 0.7 20n 0.7 20.01n 0 22n 0)

* Declarando o circuito

* Declarando transistores rede pull up (A e B em sequência e C em paralelo)
MpA vdd a n1 vdd pmos_rvt nfin = 3
MpB n1 b s vdd pmos_rvt nfin = 3
MpC vdd c s vdd pmos_rvt nfin = 3

* Declarando transistores redepull down (A e B em paralelo e C em sequência)
MnA s a n2 gnd nmos_rvt nfin = 3
MnB s b n2 gnd nmos_rvt nfin = 3
MnC n2 c gnd gnd nmos_rvt nfin = 3

* colocando capacitor na saída
cload s gnd 1f

* Simulação Transiente de 23ns com passo de 0.1ns
.tran 0.1ns 23ns 

* Medindo tempo de propagação:

* Arco 1 (0B1)
* r
.measure tran tphl_0B1 trig v(b) val= '0.5*0.7' rise= 1  targ v(s) val='0.5*0.7' fall = 1
*s
.measure tran tplh_0B1 trig v(b) val= '0.5*0.7' fall =1  targ v(s) val = '0.5*0.7' rise = 1

* Arco 2 (A01)
*t
.measure tran tphl_A01 trig v(a) val= '0.5*0.7' rise= 1  targ v(s) val='0.5*0.7' fall = 2
*v
.measure tran tplh_A01 trig v(a) val= '0.5*0.7' fall =1  targ v(s) val = '0.5*0.7' rise = 2

* Arco 3 (01C)
*w
.measure tran tphl_01C trig v(c) val= '0.5*0.7' rise= 1  targ v(s) val = '0.5*0.7' fall = 3
*x
.measure tran tplh_01C trig v(c) val= '0.5*0.7' fall =2  targ v(s) val = '0.5*0.7' rise = 3

* Arco 4 (10C)
*y
.measure tran tphl_10C trig v(c) val= '0.5*0.7' rise =2  targ v(s) val='0.5*0.7' fall = 4
*z
.measure tran tplh_10C trig v(c) val= '0.5*0.7' fall =3  targ v(s) val = '0.5*0.7' rise = 4

*Medindo energia:
.measure tran Vdd_int INTEG i(Vvdd) from= 0n to= 22n

*medindo potência total média:
.measure tran Vdd_int avg p(Vvdd) from=0n to=22n

* Fim do Arquivo SPICE
.end
* tp_XOR_finfet

*PARAMETROS
.include 7nm_TT.pm

* Declarando Fontes de tensão
Vvdd vdd gnd 0.7

* Declaração das fontes
Va a gnd PWL (0n 0 6n 0 6.01n 1 8n 0.7 8.01n 0 12n 0 12.01n 0.7 14n 0.7 14.01n 0 16n 0 16.01n 0.7 22n 0.7)
Vna na gnd PWL (0n 0.7 6n 0.7 6.01n 0 8n 0 8.01n 0.7 12n 0.7 12.01n 0 14n 0 14.01n 0.7 16n 0.7 16.01n 0 22n 0)
Vb b gnd PWL (0n 0 2n 0 2.01n 0.7 4n 0.7 4.01n 0 10n 0 10.01n 0.7 16n 0.7 16.01n 0 18n 0 18.01n 0.7 20n 0.7 20.01n 0 22n 0)
Vnb nb gnd PWL (0n 0.7 2n 0.7 2.01n 0 4n 0 4.01n 0.7 10n 0.7 10.01n 0 16n 0 16.01n 0.7 18n 0.7 18.01n 0 20n 0 20.01n 0.7 22n 0.7)

* Declarando o circuito

* Declarando transistores rede pull up 
MpA vdd a n2 vdd pmos_rvt nfin = 3
MpNA vdd na n1 vdd pmos_rvt nfin = 3
MpB n1 b s vdd pmos_rvt nfin = 3
MpNB n2 nb s vdd pmos_rvt nfin = 3

* Declarando transistores redepull down (em sequência)
MnA s a n3 gnd nmos_rvt nfin = 3
MnNA s na n4 gnd nmos_rvt nfin = 3
MnB n3 b gnd gnd nmos_rvt nfin = 3
MnNB n4 nb gnd gnd nmos_rvt nfin = 3

* colocando capacitor na saída
cload s gnd 1f

* Simulação Transiente de 23ns com passo de 0.1ns
.tran 0.1ns 23ns 

* Medindo tempo de propagação:

* Arco 1 (0B)
*r
.measure tran tplh_0B trig v(b) val= '0.5*0.7' rise =1  targ v(s) val = '0.5*0.7' rise = 1
*s
.measure tran tphl_0B trig v(b) val= '0.5*0.7' fall= 1  targ v(s) val='0.5*0.7' fall = 1

* Arco 2 (A0)
*t
.measure tran tplh_A0 trig v(a) val= '0.5*0.7' rise =1  targ v(s) val = '0.5*0.7' rise = 2
*v
.measure tran tphl_A0 trig v(a) val= '0.5*0.7' fall= 1  targ v(s) val='0.5*0.7' fall = 2

* Arco 3 (A1)
*w
.measure tran tphl_A1 trig v(a) val= '0.5*0.7' rise= 2  targ v(s) val='0.5*0.7' fall = 3
*x
.measure tran tplh_A1 trig v(a) val= '0.5*0.7' fall =2  targ v(s) val = '0.5*0.7' rise = 4

* Arco 4 (1B)
*y
.measure tran tphl_1B trig v(b) val= '0.5*0.7' rise =3  targ v(s) val='0.5*0.7' fall = 4
*z
.measure tran tplh_1B trig v(b) val= '0.5*0.7' fall =3  targ v(s) val = '0.5*0.7' rise = 5

*Medindo energia:
.measure tran Vdd_int INTEG i(Vvdd) from= 0n to= 22n


*medindo potência total média:
.measure tran Vdd_int avg p(Vvdd) from=0n to=22n

* Fim do Arquivo SPICE
.end
* tp_NAND_finfet

*PARAMETROS
.include 7nm_TT.pm

* Declarando Fontes de tensão
Vvdd vdd gnd 0.7

* Declaração das fontes
Va a gnd PWL (0n 0.7 6n 0.7 6.01n 0 8n 0 8.01n 0.7 10n 0.7)
Vb b gnd PWL (0n 0.7 2n 0.7 2.01n 0 4n 0 4.01n 0.7 10n 0.7)

* Declarando o circuito

* Declarando transistores rede pull up (em paralelo)
MpA vdd a s vdd pmos_rvt nfin = 3
MpB vdd b s vdd pmos_rvt nfin = 3

* Declarando transistores redepull down (em sequência)
MnA s a n1 gnd nmos_rvt nfin = 3
MnB n1 b gnd gnd nmos_rvt nfin = 3

* colocando capacitor na saída
cload s gnd 1f

* Simulação Transiente de 11ns com passo de 0.1ns
.tran 0.1ns 11ns 

* Medindo tempo de propagação:

* Arco 1 (1B)
.measure tran tplh_1B trig v(b) val= '0.5*0.7' fall =1  targ v(s) val = '0.5*0.7' rise = 1

.measure tran tphl_1B trig v(b) val= '0.5*0.7' rise= 1  targ v(s) val='0.5*0.7' fall = 1

* Arco 2 (A1)
.measure tran tplh_A1 trig v(a) val = '0.5*0.7' fall =1  targ v(s) val = '0.5*0.7' rise = 2

.measure tran tphl_A1 trig v(a) val ='0.5*0.7' rise= 1  targ v(s) val='0.5*0.7' fall = 2

*Medindo energia:
.measure tran Vdd_int INTEG i(Vvdd) from= 0n to = 10n

*medindo potência total média:
.measure tran Vdd_int avg p(Vvdd) from=0n to=10n

* Fim do Arquivo SPICE
.end
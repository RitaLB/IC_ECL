* tp_AoI not(A+(B.C))_finfet

*PARAMETROS
.include 7nm_TT.pm

* Declarando Fontes de tensão
Vvdd vdd gnd 0.7

* Declaração das fontes
Va a gnd PWL (0n 0 2n 0 2.01n 0.7 4n 0.7 4.01n 0 12n 0 12.01n 0.7 14n 0.7 14.01n 0 18n 0 18.01n 0.7 20n 0.7  20.01n 0 26n 0)
Vb b gnd PWL (0n 0 8n 0 8.01n 0.7 10n 0.7 10.01n 0 16n 0 16.01n 0.7 26n 0.7)
Vc c gnd PWL (0n 0 6n 0 6.01n 0.7 16n 0.7 16.01n 0 22n 0 22.01n 0.7 24n 0.7 24.01n 0 26n 0)

* Declarando o circuito

* Declarando transistores rede pull up (B e C em paralelo e A em sequência)
MpA n1 a s vdd pmos_rvt nfin = 3
MpB vdd b n1 vdd pmos_rvt nfin = 3
MpC vdd c n1 vdd pmos_rvt nfin = 3

* Declarando transistores redepull down (B e C em sequência e A em paralelo)
MnA s a gnd gnd nmos_rvt nfin = 3
MnB s b n2 gnd nmos_rvt nfin = 3
MnC n2 c gnd gnd nmos_rvt nfin = 3

* colocando capacitor na saída
cload s gnd 1f

* Simulação Transiente de 27ns com passo de 0.1ns
.tran 0.1ns 27ns 

* Medindo tempo de propagação:

* Arco 1 (A00)
.measure tran tphl_A00 trig v(a) val= '0.5*0.7' rise= 1 targ v(s) val='0.5*0.7' fall = 1

.measure tran tplh_A00 trig v(a) val= '0.5*0.7' fall =1  targ v(s) val = '0.5*0.7' rise = 1

* Arco 2 (0B1)
.measure tran tphl_0B1 trig v(b) val= '0.5*0.7' rise= 1  targ v(s) val='0.5*0.7' fall = 2

.measure tran tplh_0B1 trig v(b) val= '0.5*0.7' fall =1  targ v(s) val = '0.5*0.7' rise = 2

* Arco 3 (A01)
.measure tran tphl_A01 trig v(a) val= '0.5*0.7' rise= 2  targ v(s) val='0.5*0.7' fall = 3

.measure tran tplh_A01 trig v(a) val= '0.5*0.7' fall =2  targ v(s) val = '0.5*0.7' rise = 3

* Arco 4 (A10)
.measure tran tphl_A10 trig v(a) val= '0.5*0.7' rise= 3  targ v(s) val='0.5*0.7' fall = 4

.measure tran tplh_A10 trig v(a) val= '0.5*0.7' fall =3  targ v(s) val = '0.5*0.7' rise = 4

* Arco 5 (01C)
.measure tran tphl_01C trig v(c) val= '0.5*0.7' rise= 2  targ v(s) val='0.5*0.7' fall = 5

.measure tran tplh_01C trig v(c) val= '0.5*0.7' fall =2  targ v(s) val = '0.5*0.7' rise = 5

*Medindo energia:
.measure tran Vdd_int INTEG i(Vvdd) from= 0n to = 26n

*medindo potência total média:
.measure tran Vdd_int avg p(Vvdd) from=0n to=26n

* Fim do Arquivo SPICE
.end